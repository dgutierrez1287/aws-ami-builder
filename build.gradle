import com.bmuschko.gradle.vagrant.tasks.*
import com.amazonaws.auth.BasicAWSCredentials
import com.amazonaws.auth.DefaultAWSCredentialsProviderChain
import com.amazonaws.auth.profile.ProfileCredentialsProvider
import static groovy.io.FileType.FILES
import com.bmuschko.gradle.vagrant.tasks.VagrantSsh
import com.bmuschko.gradle.vagrant.tasks.Vagrant

import java.security.KeyManagementException;
import java.security.NoSuchAlgorithmException;
import java.security.cert.X509Certificate;

import javax.net.ssl.*;

buildscript {
  ext {
      awsVersion = '1.11.8'
  }
  repositories {
      jcenter()
      mavenCentral()
  }

  dependencies {
      classpath "com.bmuschko:gradle-vagrant-plugin:2.0"
      classpath "com.amazonaws:aws-java-sdk-core:${awsVersion}"
      classpath "com.amazonaws:aws-java-sdk-simpledb:${awsVersion}"
  }
}

apply plugin: 'com.bmuschko.vagrant'

ext {
  awsCredentials = getAwsCredentials()
  providedAwsAccessKey = getAwsAccessKey(awsCredentials)
  providedAwsSecretKey = getAwsSecretKey(awsCredentials)
  compiledToolsDir = ".compiledTools"
  plantUMLVersion = "8046"
  gradleVersion = project.hasProperty('gradleVersion') ? project.hasProperty('gradleVersion') : "3.1"
  sourceDir = project.hasProperty('sourceDir') ? project.hasProperty('sourceDir') : "/vagrant"
  groovyVersion = project.hasProperty('groovyVersion') ? project.hasProperty('groovyVersion') : "2.4.7"
  projectVersion = project.hasProperty('projectVersion') ? project.hasProperty('projectVersion') : "0.1.0"
  mysqlVersion = project.hasProperty('mysqlVersion') ? project.hasProperty('mysqlVersion') : "5.7"
}

// Vagrant plugin configuration
vagrant {
    boxDir = file('.')

    environmentVariables {
        variable 'IP', '192.168.1.33'
        variable 'OPERATINGSYSTEM', 'redhat'
    }


    installation {
        validate = false
    }
}

// Help Tasks //
task showVars (type: Task) {
    description = "Shows the variables values for the build"
    group = "Common"

    println "Main Vars:"
    println "awsVersion: ${awsVersion}"
    println "providedAWSAccessKey: ${providedAwsAccessKey}"
    println "providedAWSSecretKey: ${providedAwsSecretKey}"
    println "compiledToolsDir: ${compiledToolsDir}"
    println "plantUMLVersion: ${plantUMLVersion}"
    println "projectVersion: ${projectVersion}"
    println "sourceDir: ${sourceDir}"
    println " "
}

// Document Tasks //
task installDiagramDependencies (type: Task){ 
  description = "Installs the depdencies for building diagrams"
  group = "Documentation"
  
  def pumlFileName = "plantuml.${plantUMLVersion}.jar"
  def pumlFile = new File("${compiledToolsDir}/${pumlFileName}")
  outputs.file pumlFile

  def pumlUrl = "http://sourceforge.net/projects/plantuml/files/plantuml.${plantUMLVersion}.jar/download"

  doLast {
    logger.info("Downloading PlantUML jar ${pumlFileName}")
    logger.info("Getting PlantUML from url ${pumlUrl}")

    downloadFile(compiledToolsDir, pumlFileName, pumlUrl, "None")
  }
}

task buildDiagrams (type: Task) {
  description = "Compiles Diagrams"
  group = "Documentation"

  dependsOn 'createToolsDir'
  dependsOn 'installDiagramDependencies'

  dependsOn 'cleanDiagrams'

  doLast { 
    def fileList = []

    new File('docs/diagrams').eachFile(FILES) {
      if (it.name.endsWith('.puml')) {
        fileList = fileList + it
      }
    }

    for (file in fileList) {
      shellExecute("java -jar ${compiledToolsDir}/plantuml.${plantUMLVersion}.jar ${file}")
    }
  }
}

task cleanDiagrams (type: Delete) {
  description = "Cleans the compiled diagrams"
  group = "Documentation"

  delete fileTree(dir: 'docs/diagrams' , include: '**/*.png')
}

// Common Tasks //
task createToolsDir (type: Task) {
  description = "Creates the directory for compiled tools"
  group = "Common"
  
  def toolsDir = new File(compiledToolsDir)
  outputs.dir toolsDir

  doLast {
    logger.info("Creating compiled tools directory")
    toolsDir.mkdirs()
  }
}

// Vagrant Tasks //
task vagrantSetAWSCreds (type: VagrantSsh) {
    description = "Sets AWS credentials pulled from the local machine into vagrant"
    group = "Vagrant"
    sshCommand = "/vagrant/vagrant_scripts/utility/set_aws_creds.sh ${providedAwsAccessKey} ${providedAwsSecretKey}"
    dependsOn vagrantUp
}

task vagrantStartMysql (type: VagrantSsh) {
    description = "Sets AWS credentials pulled from the local machine into vagrant"
    group = "Vagrant"
    sshCommand = "/vagrant/vagrant_scripts/utility/start_mysql.sh ${mysqlVersion}"
    dependsOn vagrantUp
}

vagrantUp.finalizedBy(vagrantSetAWSCreds)
vagrantUp.finalizedBy(vagrantStartMysql)


// Mysql Vagrant Tasks //
task mysqlDropTables (type: VagrantSsh) {
    description = "Drops all the tables on the mysql DB"
    group = "Mysql"
    sshCommand = "/vagrant/vagrant_scripts/utility/mysql_drop_tables.sh"
}

// Docker Vagrant Tasks //
task dockerCleanImages (type: VagrantSsh) {
    description = "Cleans stale docker images and clears cache"
    group = "Docker"
    sshCommand = "/vagrant/vagrant_scripts/utility/docker_cleanup.sh images"
}

task dockerCleanContainers (type: VagrantSsh) {
    description = "Cleans stopped docker containers"
    group = "Docker"
    sshCommand = "/vagrant/vagrant_scripts/utility/docker_cleanup.sh containers"
}

task dockerCleanAll (type: VagrantSsh) {
    description = "Cleans stopped docker containers and images"
    group = "Docker"
    sshCommand = "/vagrant/vagrant_scripts/utility/docker_cleanup.sh all"
}

// Remote App Tasks //
task createAppImages (type: VagrantSsh) {
    description = "Builds the app and build imagea for the app"
    group = "App"
    sshCommand = "pushd /vagrant; gradle :app:createBuildImage; gradle :app:createAppImage; popd"
}

task createAppImage (type: VagrantSsh) {
    description = "Builds the app image for the app"
    group = "App"
    sshCommand = "pushd /vagrant; gradle :app:createAppImage; popd"
}

task createAppBuildImage (type: VagrantSsh) {
    description = "Builds the build image for the app"
    group = "App"
    sshCommand = "pushd /vagrant; gradle :app:createBuildImage; popd"
}

task buildApp (type: VagrantSsh) {
    description = "Builds the application using the build container"
    group = "App"
    sshCommand = "pushd /vagrant; gradle :app:startBuildContainer; popd"
}

task deployApp (type: VagrantSsh) {
    description = "starts the application using the app container"
    group = "App"
    sshCommand = "pushd /vagrant; gradle :app:startAppContainer; popd"
}

task buildDeployApp (type: VagrantSsh) {
    description = "runs a build with the build container and then deploys app"
    group = "App"
    sshCommand = "pushd /vagrant; gradle :app:startBuildContainer; gradle :app:startAppContainer; popd"
}


// methods //
/* 
  getAwsCredentials()
  Getting AWS credentials from either a profile, passed as properties or
  by the default chain provider 
*/
def getAwsCredentials() {

  // 3 cases for getting credentials
  // 1. if a profile name is specified in the properties file use creds from that profile
  // 2. if the accessKey and secretKey are in the properties file use those credentials
  // 3. used AWSs defualt credentals chain
  if (project.hasProperty("awsProfileName")) {
    logger.info("using the AWS credential profile provider")

    profileName = project.get("awsProfileName")
    return new ProfileCredentialsProvider(profileName).getCredentials()
  }
  else if (project.hasProperty("awsAccessKey")) {
    logger.info("using the AWS credential basic provider")

    accessKey = project.get("awsAccessKey")
    secretKey = project.get("awsSecretKey")
    return new BasicAWSCredentials(accessKey, secretKey)
  }
  else {
    logger.info("using the AWS default provider chain")

    return new DefaultAWSCredentialsProviderChain().getCredentials()
  }
}

/*
  getAwsAccessKey(awsCredentials)
  This will return the Access key part of the AWS
  credentials
*/
def getAwsAccessKey(awsCredentials) {
  
  return awsCredentials.getAWSAccessKeyId()
}

/*
  getAwsSecretKey(awsCredentials)
  This will return the Secret key part of the AWS
  credentials
*/
def getAwsSecretKey(awsCredentials) {

  return awsCredentials.getAWSSecretKey()
}

/*
  shellExecute(command)
  This will execute a command on the bash shell
*/
def shellExecute(command) {

  def buildCmd = [
    "bash",
    "-c",
    command
  ]

  Process p = buildCmd.execute()
  p.consumeProcessOutput(System.out, System.err)
  p.waitFor()

  return p.exitValue()
}

/*
  downloadFile(fileName, url)
  This will download a file to a specific file from an address,
  this will mostly be used to download any compiled dependencies
*/
def downloadFile(parentDir, fileName, url, cookie) {

  while (url) {
    new URL( url ).openConnection().with { conn ->
      conn.instanceFollowRedirects = false

      if (cookie != "None") {
        conn.setRequestProperty("Cookie", cookie)
      }
        def prevUrl = url
        url = conn.getHeaderField( "Location" )
        if (!url) {
          def file = new FileOutputStream("${parentDir}/${fileName}")
          def out = new BufferedOutputStream(file)
          out << new URL(prevUrl).openStream()
          out.close()
        }
      }
  }
}
